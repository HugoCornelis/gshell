#!/usr/bin/perl
#!/usr/bin/perl -d:ptkdb -w
#


use strict;


BEGIN
{
    #! make check

    push @INC, '../perl';

    #! make distcheck

    push @INC, '../../perl';

    #! normal run

    push @INC, './perl';

    #! after install

    push @INC, '/usr/local/glue/swig/perl';
}


use Data::Dumper;

use Getopt::Long;

use Term::ReadLine;


$SIG{__DIE__}
    = sub {
	use Carp;

	confess @_;
    };


#t does not work, I guess because of readline handling it.

$SIG{INT}
    = sub {
	use Carp;

	confess @_;

	exit 1;
    };


our $option_verbose = 'warnings';

my $exit_code = 0;


sub interprete
{
    my $line = shift;

    my $arguments = [ split /\s/, $line, ];

    #t join hash and array arguments

    # create a perl function call

    my $quoted_line = $arguments->[0];

    if ($arguments->[1])
    {
	$quoted_line
	    .= (
		join
		' ',
		"(",
		(
		 map
		 {
		     (
		      /^('|")/
		      ? "$_, "
		      : "'$_',"
		     )
		  }
		 (@$arguments)[1 .. $#$arguments]
		),
		")"
	       );

	$quoted_line =~ s/\\n/\n/g;
    }

    if ($quoted_line =~ /^\s*(#.*)?$/)
    {
	return;
    }

    {
	my $genesis_command = $arguments->[0];

	no strict "refs";

	if (!exists ((\%{"::"})->{"GENESIS3::"}->{"Commands::"}->{$genesis_command}))
	{
	    print "*** Error: command $genesis_command not found,\n*** Error: use 'list commands' to get a list of available commands,\n*** Error: use the help function to obtain help about each command\n";

	    return;
	}
    }

    my $package = "GENESIS3::Commands::";

    $quoted_line =~ s/^\s*/$package/;

    my $result = eval($quoted_line);

    if ($@)
    {
	warn $@;

	print Data::Dumper->Dump( [ $result, ], [ 'Result', ]);
    }

    if ($result =~ /^\*\*\* Ok/)
    {
    }
    else
    {
	print "$result\n";
    }
}


sub loop
{
    my $historyfile = $ENV{HOME} . '/.phistory';

    my $term = Term::ReadLine->new('genesis > ');

    if (open H, $historyfile)
    {
	my %h;

	my @h = <H>;
	chomp @h;
	close H;

	$h{$_} = 1 foreach @h;
	$term->addhistory($_) foreach keys %h;
    }

    while ( defined ($_ = $term->readline("genesis > ")) )
    {
	my $line = $_;

	interprete($line);

	{
	    open H, ">>$historyfile";
	    print H "$line\n";
	    close H;
	}

	$term->addhistory($line) if /\S/;
    }
}


sub main
{
    if (!$ENV{NEUROSPACES_NMC_USER_MODELS}
	and !$ENV{NEUROSPACES_NMC_PROJECT_MODELS}
	and !$ENV{NEUROSPACES_NMC_SYSTEM_MODELS}
	and !$ENV{NEUROSPACES_NMC_MODELS})
    {
	$ENV{NEUROSPACES_NMC_MODELS} = '/usr/local/neurospaces/models/library';
    }

    read_cmd_line();

    require GENESIS3;

    GENESIS3::Commands::set_verbose($option_verbose);

    GENESIS3::header();

    STDOUT->autoflush(1);

    foreach my $script (@ARGV)
    {
	use IO::File;

	my $file = IO::File->new("<$script");

	my $lines = [ <$file>, ];

	foreach my $line (@$lines)
	{
	    #t do error processing

	    interprete($line);
	}
    }

    loop();
}


sub read_cmd_line
{
    my $option_help;
    my $option_version;

    my $result
	= GetOptions
	    (
	     "help!" => \$option_help,
	     "v|verbose=s" => \$option_verbose,
	     "version" => \$option_version,
	    );

    if ($option_version)
    {
	require GENESIS3;

	my $version = GENESIS3::version();

	print $version . "\n";

	exit 1;
    }

    if ($option_help)
    {
	print
	    "
$0 <options>

$0: GENESIS 3 shell.

options:
    --help               print usage information.
    --verbose            set verbosity level ('errors', 'warnings', 'information', 'debug', default is 'warnings').
    --version            give version information.
";

	exit 1;
    }

}


main();


exit $exit_code;


